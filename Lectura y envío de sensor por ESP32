#include <Arduino.h>
#include <Wire.h>

#define AHT10_ADDR 0x38
#define SDA_PIN 21
#define SCL_PIN 22

// put function declarations here:

// ----- Inicializar AHT10 -----
void aht10_init() {
  Wire.beginTransmission(AHT10_ADDR);
  Wire.write(0xE1);  // Comando de inicialización
  Wire.write(0x08);
  Wire.write(0x00);
  Wire.endTransmission();
  delay(100);
}

// ----- Leer datos del sensor AHT10 -----
bool aht10_read(float &temperature, float &humidity) {
  Wire.beginTransmission(AHT10_ADDR);
  Wire.write(0xAC);
  Wire.write(0x33);
  Wire.write(0x00);
  Wire.endTransmission();
  delay(80);

  uint8_t count = Wire.requestFrom(AHT10_ADDR, (uint8_t)6);
  if (count != 6) return false;

  uint8_t data[6];
  for (int i = 0; i < 6; i++) data[i] = Wire.read();

  uint32_t rawHum = ((uint32_t)data[1] << 16) |
                    ((uint32_t)data[2] << 8) |
                     data[3];
  rawHum >>= 4;
  humidity = (rawHum / 1048576.0) * 100.0;

  uint32_t rawTemp = ((uint32_t)(data[3] & 0x0F) << 16) |
                      ((uint32_t)data[4] << 8) |
                       data[5];
  temperature = (rawTemp / 1048576.0) * 200.0 - 50.0;

  return true;
}

void setup() {
  Serial.begin(115200);
  Serial2.begin(115200, SERIAL_8N1, 16, 17);   // UART hacia STM32
  Wire.begin(SDA_PIN, SCL_PIN);
  delay(40);
  aht10_init();

  Serial.println("ESP32 listo, esperando comando por UART2...");
}

void loop() {
  // ¿Hay comando desde el STM32?
  if (Serial2.available() > 0) {
    char lectura = Serial2.read();
    Serial.print("Recibido por UART2: ");
    Serial.println(lectura);

    if (lectura == '1') {   // STM32 solicita lectura del sensor
      float temp, hum;
      bool ok = aht10_read(temp, hum);

            if (!ok) {
        Serial.println("Fallo al leer AHT10");
        Serial2.println("ERR");
      } else {
        Serial.print("Enviando: ");
        Serial.print(hum,2);
        Serial.print(",");
        Serial.println(temp,2);

        Serial2.print(hum, 2);
        Serial2.print(",");
        Serial2.println(temp, 2);
      }
    }
  }
}
